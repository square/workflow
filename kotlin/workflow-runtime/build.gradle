/*
 * Copyright 2019 Square Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

// Benchmark plugins.
apply plugin: 'me.champeau.gradle.jmh'
// If this plugin is not applied, IntelliJ won't see the JMH definitions for some reason.
apply plugin: 'idea'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

apply from: rootProject.file('.buildscript/configure-maven-publish.gradle')

// Benchmark configuration.
jmh {
  include = ['.*']
  duplicateClassesStrategy = 'warn'
}
configurations {
  jmh.with {
    attributes.attribute(Usage.USAGE_ATTRIBUTE, getObjects().named(Usage, Usage.JAVA_RUNTIME))
  }
}
compileJmhKotlin {
  kotlinOptions {
    // Give the benchmark code access to internal definitions.
    // Can't use sourceSets.main.runtimeClasspath since it would have to be evaluated eagerly.
    freeCompilerArgs += "-Xfriend-paths=${compileKotlin.destinationDir}"
  }
}

dependencies {
  compileOnly deps.annotations.intellij

  api project(':workflow-core')
  api deps.kotlin.stdLib.jdk6
  api deps.kotlin.coroutines.core

  testImplementation deps.kotlin.test.jdk
  testImplementation deps.kotlin.reflect

  // These dependencies will be available on the classpath for source inside src/jmh.
  jmh deps.kotlin.stdLib.jdk6
  jmh deps.jmh.core
  jmh deps.jmh.generator
}
