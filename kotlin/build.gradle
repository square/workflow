/*
 * Copyright 2017 Square Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  ext.versions = [
      'targetSdk': 29,
      'dokka': '0.9.18',
      'kotlin': '1.3.41',
      'kotlinCoroutines': '1.3.0-RC2',
  ]

  rootProject.ext.defaultAndroidConfig = {
    compileSdkVersion versions.targetSdk
    buildToolsVersion '29.0.0'

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
      minSdkVersion 21
      targetSdkVersion versions.targetSdk
      versionCode 1
      versionName "1.0"
    }
  }

  ext.deps = [
      'android_gradle_plugin': "com.android.tools.build:gradle:3.5.0-rc03",

      'androidx': [
          'annotations': "androidx.annotation:annotation:1.1.0",
          'appcompat': "androidx.appcompat:appcompat:1.0.2",
          'constraint_layout': "androidx.constraintlayout:constraintlayout:1.1.3",
          'lifecycle': "androidx.lifecycle:lifecycle-extensions:2.0.0",
          'lifecycleReactivestreams': "androidx.lifecycle:lifecycle-reactivestreams-ktx:2.0.0",
          // Note that we're not using the actual androidx material dep yet, it's still alpha.
          'material': "com.google.android.material:material:1.0.0",
          'transition': "androidx.transition:transition:1.1.0",
      ],

      'coordinators': "com.squareup.coordinators:coordinators:0.4",
      'rxandroid2': "io.reactivex.rxjava2:rxandroid:2.1.0",
      'timber': "com.jakewharton.timber:timber:4.7.1",

      'kotlin': [
          'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
          'stdLib': [
              'common': "org.jetbrains.kotlin:kotlin-stdlib-common",
              'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8",
              'jdk7': "org.jetbrains.kotlin:kotlin-stdlib-jdk7",
              'jdk6': "org.jetbrains.kotlin:kotlin-stdlib"
          ],
          'coroutines': [
              'android': "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlinCoroutines}",
              'core': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinCoroutines}",
              'rx2': "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:${versions.kotlinCoroutines}",
              'test': "org.jetbrains.kotlinx:kotlinx-coroutines-test:${versions.kotlinCoroutines}"
          ],
          'reflect': "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}",
          'test': [
              'common': "org.jetbrains.kotlin:kotlin-test-common",
              'annotations': "org.jetbrains.kotlin:kotlin-test-annotations-common",
              'jdk': "org.jetbrains.kotlin:kotlin-test-junit",
              'mockito': "com.nhaarman:mockito-kotlin-kt1.1:1.6.0"
          ]
      ],
      'dokkaAndroid': "org.jetbrains.dokka:dokka-android-gradle-plugin:${versions.dokka}",
      'dokka': "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}",
      // Source of "API 'variant.getJavaCompile()' is obsolete" warning, because of course it is.
      'mavenPublish': "com.vanniktech:gradle-maven-publish-plugin:0.8.0",
      'ktlint': "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:5.1.0",
      'lanterna': "com.googlecode.lanterna:lanterna:3.0.1",
      'detekt': "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.0.0-RC16",
      'okio': "com.squareup.okio:okio:2.3.0",
      'rxjava2':[
          'rxjava2': "io.reactivex.rxjava2:rxjava:2.2.11",
          'extensions': "com.github.akarnokd:rxjava2-extensions:0.20.10"
      ],
      'annotations': [
          'intellij': "org.jetbrains:annotations:17.0.0",
      ],
      'test': [
          'truth': "com.google.truth:truth:1.0",
          'junit': "junit:junit:4.12",
          'mockito': "org.mockito:mockito-core:3.0.0",
          'hamcrestCore': "org.hamcrest:hamcrest-core:2.1"
      ]
  ]

  dependencies {
    classpath deps.android_gradle_plugin
    classpath deps.detekt
    classpath deps.dokka
    classpath deps.dokkaAndroid
    classpath deps.kotlin.gradlePlugin
    classpath deps.ktlint
    classpath deps.mavenPublish
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
    google()
  }
}

subprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
  }

  apply plugin: "org.jlleitschuh.gradle.ktlint"
  apply plugin: "io.gitlab.arturbosch.detekt"
  afterEvaluate {
    project.tasks.findByName('check')?.dependsOn 'detekt'
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
      kotlinOptions.allWarningsAsErrors = true

      // Don't panic, all this does is allow us to use the @Experimental meta-annotation.
      // to define our own experiments.
      freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
    }
  }
}
