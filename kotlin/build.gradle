/*
 * Copyright 2017 Square Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  ext.versions = [
      'agp': '3.3.2',
      'buildTools': '28.0.3',
      'compileSdk': '28',
      'lifecycle': '1.1.1',
      'minSdk': 21,
      'targetSdk': 28,
      'sourceCompatibility': JavaVersion.VERSION_1_8,
      'targetCompatibility': JavaVersion.VERSION_1_8,

      'coordinators': '0.4',
      'rxandroid2': '2.1.0',
      'support': '28.0.0',
      'timber': '4.7.1',

      'assertj': '2.6.0',
      'detektPlugin': '1.0.0-RC11',
      'dokka': '0.9.16',
      'hamcrest': '1.3',
      'intellijAnnotations': '13.0',
      'junit': '4.12',
      'kotlin': '1.3.30',
      'kotlinCoroutines': '1.2.0',
      'ktlintPlugin': '5.1.0',
      'mavenPublishPlugin': '0.6.0',
      'mockito': '2.7.5',
      'mockitoKotlin': '1.5.0',
      'rxjava2Extensions': '0.20.4',
  ]

  ext.deps = [
      'android_gradle_plugin': "com.android.tools.build:gradle:${versions.agp}",

      'appcompatv7': "com.android.support:appcompat-v7:${versions.support}",
      'coordinators': "com.squareup.coordinators:coordinators:${versions.coordinators}",
      'constraint_layout': "com.android.support.constraint:constraint-layout:1.1.0",
      'design': "com.android.support:design:${versions.support}",
      'lifecycle': "android.arch.lifecycle:extensions:${versions.lifecycle}",
      'rxandroid2': "io.reactivex.rxjava2:rxandroid:${versions.rxandroid2}",
      'timber': "com.jakewharton.timber:timber:${versions.timber}",
      'transition': "com.android.support:transition:${versions.support}",

      'kotlin': [
          'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
          'stdLib': [
              'common': "org.jetbrains.kotlin:kotlin-stdlib-common",
              'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8",
              'jdk7': "org.jetbrains.kotlin:kotlin-stdlib-jdk7",
              'jdk6': "org.jetbrains.kotlin:kotlin-stdlib"
          ],
          'coroutines': [
              'android': "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlinCoroutines}",
              'core': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinCoroutines}",
              'rx2': "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:${versions.kotlinCoroutines}"
          ],
          'reflect': "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}",
          'test': [
              'common': "org.jetbrains.kotlin:kotlin-test-common",
              'annotations': "org.jetbrains.kotlin:kotlin-test-annotations-common",
              'jdk': "org.jetbrains.kotlin:kotlin-test-junit",
              'mockito': "com.nhaarman:mockito-kotlin-kt1.1:${versions.mockitoKotlin}"
          ]
      ],
      'dokkaAndroid': "org.jetbrains.dokka:dokka-android-gradle-plugin:${versions.dokka}",
      'dokka': "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}",
      'mavenPublish': "com.vanniktech:gradle-maven-publish-plugin:${versions.mavenPublishPlugin}",
      'ktlint': "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:${versions.ktlintPlugin}",
      'detekt': "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detektPlugin}",
      'okio': "com.squareup.okio:okio:2.2.2",
      'rxjava2':[
          'rxjava2': "io.reactivex.rxjava2:rxjava:2.2.3",
          'interop': "com.github.akarnokd:rxjava2-interop:0.11.4",
          'extensions': "com.github.akarnokd:rxjava2-extensions:${versions.rxjava2Extensions}"
      ],
      'annotations': [
          'intellij': "org.jetbrains:annotations:${versions.intellijAnnotations}",
          'androidSupport': "com.android.support:support-annotations:26.0.2",
      ],
      'test': [
          'assertj': "org.assertj:assertj-core:${versions.assertj}",
          'junit': "junit:junit:${versions.junit}",
          'mockito': "org.mockito:mockito-core:${versions.mockito}",
          'hamcrestCore': "org.hamcrest:hamcrest-core:${versions.hamcrest}"
      ]
  ]

  dependencies {
    classpath deps.android_gradle_plugin
    classpath deps.detekt
    classpath deps.dokka
    classpath deps.dokkaAndroid
    classpath deps.kotlin.gradlePlugin
    classpath deps.ktlint
    classpath deps.mavenPublish
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
    google()
  }
}

rootProject.ext.defaultDokkaConfig = {
    outputFormat = 'gfm'
    outputDirectory = "$buildDir/docs"
}

rootProject.ext.defaultAndroidConfig = {
  compileSdkVersion versions.targetSdk
  buildToolsVersion versions.buildTools

  compileOptions {
    sourceCompatibility versions.sourceCompatibility
    targetCompatibility versions.targetCompatibility
  }

  defaultConfig {
    minSdkVersion versions.minSdk
    targetSdkVersion versions.targetSdk
    versionCode 1
    versionName "1.0"
  }
}

allprojects {
  group = GROUP
  version = VERSION_NAME
}

subprojects {
  repositories {
    mavenCentral()
    jcenter()
    google()
  }

  apply plugin: "org.jlleitschuh.gradle.ktlint"
  apply plugin: "io.gitlab.arturbosch.detekt"
  afterEvaluate {
    project.tasks.findByName('check')?.dependsOn 'detekt'
  }
}
