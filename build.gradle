buildscript {
  ext.versions = [
      'kotlin': '1.2.61',
      'kotlinCoroutines': '0.26.1',
      'dokka': '0.9.16',
      'junit': '4.12',
      'assertj': '2.6.0',
      'mavenPublishPlugin': '0.6.0',
      'ktlintPlugin': '5.1.0',
      'detektPlugin': '1.0.0-RC11',
      'mockitoKotlin': '1.5.0',
      'mockito': '2.7.5',
      'intellijAnnotations': '13.0',
      'hamcrest': '1.3',
      'rxjava2Extensions': '0.20.4'
  ]

  ext.deps = [
      'kotlin': [
          'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
          'stdLib': [
              'common': "org.jetbrains.kotlin:kotlin-stdlib-common",
              'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8",
              'jdk7': "org.jetbrains.kotlin:kotlin-stdlib-jdk7",
              'jdk6': "org.jetbrains.kotlin:kotlin-stdlib"
          ],
          'coroutines': [
              'core': "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.kotlinCoroutines}",
              'rx2': "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:${versions.kotlinCoroutines}"
          ],
          'reflect': "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}",
          'test': [
              'common': "org.jetbrains.kotlin:kotlin-test-common",
              'annotations': "org.jetbrains.kotlin:kotlin-test-annotations-common",
              'jdk': "org.jetbrains.kotlin:kotlin-test-junit",
              'mockito': "com.nhaarman:mockito-kotlin-kt1.1:${versions.mockitoKotlin}"
          ]
      ],
      'dokka': "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}",
      'mavenPublish': "com.vanniktech:gradle-maven-publish-plugin:${versions.mavenPublishPlugin}",
      'ktlint': "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:${versions.ktlintPlugin}",
      'detekt': "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detektPlugin}",
      'okio': "com.squareup.okio:okio:1.13.0",
      'rxjava2':[
          'rxjava2': "io.reactivex.rxjava2:rxjava:2.2.3",
          'interop': "com.github.akarnokd:rxjava2-interop:0.11.4",
          'extensions': "com.github.akarnokd:rxjava2-extensions:${versions.rxjava2Extensions}"
      ],
      'annotations': [
          'intellij': "org.jetbrains:annotations:${versions.intellijAnnotations}",
          'androidSupport': "com.android.support:support-annotations:26.0.2",
      ],
      'test': [
          'assertj': "org.assertj:assertj-core:${versions.assertj}",
          'junit': "junit:junit:${versions.junit}",
          'mockito': "org.mockito:mockito-core:${versions.mockito}",
          'hamcrestCore': "org.hamcrest:hamcrest-core:${versions.hamcrest}"
      ]
  ]

  dependencies {
    classpath deps.kotlin.gradlePlugin
    classpath deps.dokka
    classpath deps.mavenPublish
    classpath deps.ktlint
    classpath deps.detekt
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

allprojects {
  group = GROUP
  version = VERSION_NAME
}

subprojects {
  repositories {
    mavenCentral()
    jcenter()
    google()
  }

  // Static analysis
  apply plugin: "org.jlleitschuh.gradle.ktlint"
  apply plugin: "com.vanniktech.maven.publish"

  apply plugin: "io.gitlab.arturbosch.detekt"
  afterEvaluate {
    project.tasks.getByName('check').dependsOn 'detekt'
  }
}
